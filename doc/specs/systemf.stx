// s is the target scope of a path p
tgt(p,s) :- p match
  { {x}      end(x)       -> s = x
  | {x,l,xs} edge(x,l,xs) -> tgt(xs,s)
  }.

// s is the source scope of a path p
src(p,s) :- p match
  { {x}      end(x)       -> s = x
  | {x,l,xs} edge(x,l,xs) -> s = x
  }.

// d is the datum of the target of p
datum(p,d) :- {s} tgt(p, s), s -> d.

// declares term variable (x:t) in scope s
decl(s,x,t)   :- {sd} new sd, s -[ Decl ]-> sd, sd -> pair(x,t).
newTVar(s,X,Y) :- new Y, s -[ TyDecl ]-> Y, Y -> X.

// (x:ty) is the only reachable declaration over `P*`Decl with name x from scope s
lookup(s,x,ty) :- {dec, ans, ps, qs, p}
    query s `P*`Decl as ans
  , filter ans ({y, ty} pair(y, ty) where y = x) ps
  , min ps lexico(Decl < P) qs
  , only(qs, p)
  , datum(p, dec)
  , dec match { {y, typ} pair(y,typ) -> ty = typ }.

lookupTVar(s,X,T) :- {tys, matches, closest, p, X'}
        query s `P*`TyDecl as tys
      , filter tys ({Z} Z where Z = X) matches
      , min matches lexico(NewTVar < P) closest
      , only(closest, p)
      , tgt(p, X')
      , T = TVar(X').

typeOf(s, e, ty) :- e match
  // variables
  { {x} var(x) -> lookup(s, x, ty)

  // lambda abstraction
  | {x, ta, TA, body, sbody, TB} lam(x, ta, body) ->
          new sbody
	, sbody -[ P ]-> s
	, decl(sbody, x, TA)
	, typeOf(sbody, body, TB)
	, interp(s, ta, TA)
	, ty = Fun(TA, TB)

  // lambda application
  | {f, e, ta} app(f, e) ->
          typeOf(s, f, Fun(ta, ty))
	, typeOf(s, e, ta)

  // type abstraction
  | {X, body, sbody, TA, Q} Lam(X, body) ->
          new sbody
	, sbody -[ P ]-> s
	, newTVar(sbody, X, Q)
	, typeOf(sbody, body, TA)
	, ty = Forall(Q, TA)

  // type application
  | {X, F, t, T, A} App(F, t) ->
          interp(s, t, T)
        , typeOf(s, F, Forall(X, A))
	, subst(X, A, T, ty)

  // natural numbers
  | {} zero()  -> ty = Nat()
  | {x} suc(x) -> typeOf(s, x, Nat()), ty = Nat()
  }.

// interpret syntactic types
interp(s,ty,ty') :- ty match 
  { {X} tvar(X) -> {tys, matches, closest, p}
        lookupTVar(s, X, ty')
  | {a,b} fun(a, b) -> {A, B}
        interp(s, a, A)
      , interp(s, b, B)
      , ty' = Fun(A, B)
  | {X,a} forall(X, a) -> {A, s', X'}
        new s'
      , s' -[ P ]-> s
      , newTVar(s', X, X')
      , interp(s', a, a)
      , ty' = Forall(X', A)
  | {} nat() -> ty' = Nat()
  }.

// Substitute eagerly in a semantic type.
// Because we use semantic types this is trivally capture avoiding.
subst(N, t, s, out) :- t match
  { {alpha, r, r'} Forall(alpha, r) ->
        subst(N, r, s, r')
      , out = Forall(alpha, r')
  | {a, b, A, B} Fun(a, b) ->
        subst(N, a, s, A)
      , subst(N, b, s, B)
      , out = Fun(A, B)
  | {}  Nat() ->
        out = Nat()
  | {X} TVar(X) where X = N ->
        out = s
  | {X} TVar(X) ->
        out = X
  }.

// ty is the type of *closed* lambda expression e
typeClosed(e, ty) :- {top} new top, typeOf(top, e, ty).

//---------------------------------------------------------------------
// some unit tests

// [] ⊢ (Λ X . λ x:X . zero) [ nat ] : nat → nat
testApp1(ty) :- typeClosed(
  App(
   Lam(X(),
    lam(x(), tvar(X()), zero())),
   nat()),
  ty).

// [] ⊢ (Λ X . λ x:X . Λ Y . λ y:Y. zero) [ nat ] : nat → ∀ Y . Y → nat
testApp2(ty) :- typeClosed(
  App(
   Lam(X(),
    lam(x(), tvar(X()),
     Lam(Y(),
      lam(y(), tvar(Y()), zero())))),
   nat()),
  ty).

// [] ⊢ (Λ X . Λ Y . λ y:Y . zero) [ nat ] : ∀ Y . Y → nat
testApp3(ty) :- typeClosed(
  App(
   Lam(X(),
    Lam(Y(),
     lam(y(), tvar(Y()), zero()))),
   nat()),
  ty).

// [] ⊢ (Λ X . λ x:nat. Λ X . λ (y:X) . zero) [ nat ] : nat → ∀ X . X → nat
testApp4(ty) :- typeClosed(
  App(
   Lam(X(),
    lam(x(), nat(),
     Lam(X(),
      lam(y(), tvar(X()), zero())))),
    nat()),
  ty).

// [] ⊢ Λ X . (Λ Y. λ y:Y . zero) [ X ] : ∀ X . X → nat
testApp5(ty) :- typeClosed(
  Lam(X(),
    App(
      Lam(Y(), lam(y(), tvar(Y()), zero())),
      tvar(X())
    )
  ),
  ty
).
