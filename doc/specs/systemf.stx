// s is the target scope of a path p
tgt(p,s) :- p match
  { {x}      end(x)       -> s = x
  | {x,l,xs} edge(x,l,xs) -> tgt(xs,s)
  }.

// s is the source scope of a path p
src(p,s) :- p match
  { {x}      end(x)       -> s = x
  | {x,l,xs} edge(x,l,xs) -> s = x
  }.

// d is the datum of the target of p
datum(p,d) :- {s} tgt(p, s), s -> d.

// declares (x:t) in scope s
decl(s,x,t)   :- {sd} new sd, s -[ Decl   ]-> sd, sd -> pair(x,t).
tydecl(s,X,Y) :- new Y, s -[ TyDecl ]-> Y, Y -> X.

// (x:ty) is the only reachable declaration over `P*`Decl with name x from scope s
lookup(s,x,ty) :- {dec, ans, ps, qs, p}
    query s `P*`Decl as ans
  , filter ans ({y, ty} pair(y, ty) where y = x) ps
  , min ps lexico(Decl < P) qs
  , only(qs, p)
  , datum(p, dec)
  , dec match { {y, typ} pair(y,typ) -> ty = typ }.

// typeOf(s, e, e', A) = s ⊢ e ∼ e' : A 
// i.e. e and e' are equivalent terms at type A in scope s
typeOf(s, e, ty) :- e match
  // variables
  { {x} var(x) -> lookup(s, x, ty)

  // lambda abstraction and application
  | {x, ta, TA, body, sbody, TB} lam(x, ta, body) ->
          new sbody
	, sbody -[ P ]-> s
	, decl(sbody, x, TA)
	, typeOf(sbody, body, TB)
	, interp(s, ta, TA)
	, ty = Fun(TA, TB)
  | {f, e, ta} app(f, e) ->
          typeOf(s, f, Fun(ta, ty))
	, typeOf(s, e, ta)

  // type abstraction and application
  | {X, body, sbody, TA, Q} Lam(X, body) ->
          new sbody
	, sbody -[ P ]-> s
	, tydecl(sbody, X, Q)
	, typeOf(sbody, body, TA)
	, ty = Forall(Q, TA)
  | {X, F, t, T, A} App(F, t) ->
          interp(s, t, T)
        , typeOf(s, F, Forall(X, A))
	, subst(X, A, T, ty)

  // natural numbers
  | {} zero()  -> ty = Nat()
  | {x} suc(x) -> typeOf(s, x, Nat()), ty = Nat()
  }.

// interpret syntactic types
interp(s,ty,ty') :- ty match 
  { {X} tvar(X) -> {tys, matches, closest, p, X'}
        query s `P*`TyDecl as tys
      , filter tys ({Z} Z where Z = X) matches
      , min matches lexico(TyDecl < P) closest
      , only(closest, p)
      , tgt(p, X')
      , ty' = TVar(X')
  | {a,b} fun(a, b) -> {a', b'}
        interp(s, a, a')
      , interp(s, b, b')
      , ty' = Fun(a', b')
  | {X,a} forall(X, a) -> {a', s', X'}
        new s'
      , s' -[ P ]-> s
      , tydecl(s', X, X')
      , interp(s', a, a')
      , ty' = Forall(X', a')
  | {} nat() -> ty' = Nat()
  }.

// substitute eagerly in a semantic type
subst(N, t, s, out) :- t match
  { {alpha, r, r'} Forall(alpha, r) ->
        subst(N, r, s, r')
      , out = Forall(alpha, r')
  | {a, b, a', b'} Fun(a, b) ->
        subst(N, a, s, a')
      , subst(N, b, s, b')
      , out = Fun(a', b')
  | {}  Nat() ->
        out = Nat()
  | {X} TVar(X) where X = N ->
        out = s
  | {X} TVar(X) ->
        out = X
  }.

// ty is the type of *closed* lambda expression e
typeClosed(e, ty) :- {top} new top, typeOf(top, e, ty).

// [] ⊢ (Λ X . λ x:X . zero) [ nat ] : nat → nat
testApp1(ty) :- typeClosed(
  App(
   Lam(X(),
    lam(x(), tvar(X()), zero())),
   nat()),
  ty).

// [] ⊢ (Λ X . λ x:X . Λ Y . λ y:Y. zero) [ nat ] : nat → ∀ Y . Y → nat
testApp2(ty) :- typeClosed(
  App(
   Lam(X(),
    lam(x(), tvar(X()),
     Lam(Y(),
      lam(y(), tvar(Y()), zero())))),
   nat()),
  ty).

// [] ⊢ (Λ X . Λ Y . λ y:Y . zero) [ nat ] : ∀ Y . Y → nat
testApp3(ty) :- typeClosed(
  App(
   Lam(X(),
    Lam(Y(),
     lam(y(), tvar(Y()), zero()))),
   nat()),
  ty).

// [] ⊢ (Λ X . λ x:nat. Λ X . λ (y:X) . zero) [ nat ] : nat → ∀ X . X → nat
testApp4(ty) :- typeClosed(
  App(
   Lam(X(),
    lam(x(), nat(),
     Lam(X(),
      lam(y(), tvar(X()), zero())))),
    nat()),
  ty).

// [] ⊢ Λ X . (Λ Y. λ y:Y . zero) [ X ] : ∀ X . X → nat
testApp5(ty) :- typeClosed(
  Lam(X(),
    App(
      Lam(Y(), lam(y(), tvar(Y()), zero())),
      tvar(X())
    )
  ),
  ty
).
